% -----------------------------------------------------------------------------
% WireWorld : Wireworld cellular automata
%             https://en.wikipedia.org/wiki/Wireworld
%
% Description :
% - WireWorld is based on 3-state mineral agents: 'conductor', 'head', 'tail'
% - 'conductor' simulates path for moving electrons
% - 'head' and 'tail' are the two adjacent parts of a moving electron
% - 'head' generates an 'electric' field only captured by 'conductor'
% -----------------------------------------------------------------------------

world 7 13 #000

mineral conductor #FF0
var active                   % default: InitValue=0 TimeStepValue=0
sensor active electric 1
status active<1 conductor
status 0<active<3 head       % 'conductor' -> 'head' only when 'active' == 1 or 2
status active>2 conductor

mineral tail #F00
status conductor             % 'tail' -> 'conductor' without condition

mineral head #00F
var electric 2               % default: TimeStepValue=0
field electric -1
status tail                  % 'head' -> 'tail' without condition

% set two electrons moving on a single horizontal wire (use slice notation)

agent tail (1,1) (5,2) 
agent head (1,2) (5,1)
agent conductor (1,3:11)
agent conductor (5,3) (5,4) (5,6:11)
agent conductor (2,1) (4,1)
agent conductor (2,5) (4,5) (3,4:6)
% -----------------------------------------------------------------------------

END