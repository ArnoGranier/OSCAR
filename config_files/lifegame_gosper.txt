% -----------------------------------------------------------------------------
% LifeGame : Conway's Game of Life
%            http://en.wikipedia.org/wiki/Conway's_Game_of_Life
%
% Description :
% - LifeGame is based on 2-state mineral agents: 'live' and 'dead'
% - 'live' agents generate a 'life' field on their 8 neighbouring cells
% - 'dead' and 'live' agents have a 'neighbour' sensor for this 'life' field
% -----------------------------------------------------------------------------

world 30 50 #BBB

mineral dead #FFF
var neighbour               % default: InitValue=0 TimeStepValue=0
status neighbour<3 dead   % status lines are evaluated in given order
status neighbour=3 live   % 'dead' -> 'live' only when 'neighbour' == 3
status neighbour>3 dead
sensor neighbour life 1     % 'life' field is scale by 1 and stored in 'neighbour'

mineral live #000
var life 2                  % default: TimeStepValue=0
var neighbour               % default: InitValue=0 TimeStepValue=0
status neighbour<2 dead
status 1<neighbour<4 live   % 'live' -> 'live' only when 'neighbour' == 2 or 3
status neighbour>3 dead
field life -1
sensor neighbour life 1

% initial configuration for the pentadecathlon oscillator (period 15)
agent dead all
agent live (6,5) (6,6) (7,5) (7,6)
agent live (6,14) (6,15) (7,13) (7,15) (8,13) (8,14)
agent live (8,21) (8,22) (9,21) (9,23) (10,21)
agent live (4,28) (4,29) (5,27) (5,29) (6,27) (6,28)
agent live (4,39) (4,40) (5,39) (5,40)
agent live (16,29) (16,30) (16,31) (17,29) (18,30)
agent live (11,40) (11,41) (12,40) (12,42) (13,40)
% -----------------------------------------------------------------------------

END